begin  <* hjchio: Algol {kvivalent til 'checkio'.
                  06-1987 /hj. *>

   zone zbs,zcom(20*128,1,stderror); <* bestemmer max. blokl{ngde pr. overf. *>
   integer i,j,k,r;
   integer array zia1(1:20),ia(1:8),ans(1:9),sia1(1:12),ans1(1:9);
   array proc(1:2);


   integer procedure send(z,ans);
   zone z; integer array ans;

   <* sender message i z share nr. 1 og venter p} answer. Fors|ger
      evt. at reparere fejlene 'message rejected' (ved at reservere processen)
      samt 'process does not exist' (ved at oprette arealproces)
   *>

   begin
      integer res;

      if monitor(16,z,1,ans)=0 then system(9,6,<:break:>);
      <* send message, resultat=0 hvis buffer claim overskredet *>

      res:=monitor(18,z,1,ans);
      <* wait answer, resultatet i 'res', svaret i 'ans' hvis res=1. *>

      if res=5 then
      begin  <* process unknown, der fors|ges oprettet en arealproces *>
         i:=monitor(52,z,0,ans);  <* create area process *>
         if i=0 then
         begin
            write(out,<:* create area process ok:>,"nl",2);
            res:=send(z,ans); <* pr|v igen, kan evt. give 'rejected' *>
         end else write(out,<:*** create area process result:>,i,"nl",2);
      end else

      if res=2 then
      begin  <* message rejected, fors|g reservation *>
         i:=monitor(8,z,0,ans);
         if i=0 then
         begin
            write(out,<:* reserve process ok:>,"nl",2);
            res:=send(z,ans);
         end else write(out,<:*** reserve process result:>,i,"nl",2);
      end;

      send:=res;
   end proc. send;



   trapmode:=1 shift 9;  <* undertryk 'break'-alarm *>
   trap(TL);

   system(4,1,proc);  <* 1. fp-parameter i programkaldet *>
   open(zbs,0,proc,0);
   open(zcom,0,<::>,0);
   close(zbs,false);
   close(zcom,false);

   while true do
   begin

      i:=monitor(20,zcom,j,ia);           <* wait message *>
      if i<=0 then system(9,i,<:mess.:>);

      if ia(1)=3 shift 12 then
      begin
         getzone6(zbs,zia1);
         getshare6(zbs,sia1,1);

         sia1(4):=ia(1);                 <* operation shift 12 + mode *>
         sia1(5):=zia1(19)+sia1(2);      <* first hw = bufferbase + first shared *>
         sia1(6):=sia1(5)+ia(3)-ia(2)+1; <* last hw = first hw + 'blocklength' *>

         for i:=4,5,6,7,8 do sia1(i+3):=ia(i);

         setshare6(zbs,sia1,1);

         i:=send(zbs,ans1);
         getzone6(zcom,zia1);
         zia1(14):=zia1(19);   <* record base = bufferbase *>
         zia1(15):=zia1(14)+ans1(2)+1;    <* last hw = record base + rec.length +1 *>
         setzone6(zcom,zia1);
         tofrom(zcom,zbs,ans1(2));
         monitor(70,zcom,j,ans);
         ans(9):=i;          <* result *>
      end else

      if ia(1)=5 shift 12 then
      begin
         getzone6(zcom,zia1);
         zia1(14):=zia1(19);
         zia1(15):=zia1(14)+ia(3)-ia(2)+1;
         setzone6(zcom,zia1);
         monitor(70,zcom,j,ans);
         tofrom(zbs,zcom,ans(2));
         getzone6(zbs,zia1);
         getshare6(zbs,sia1,1);

         sia1(4):=ia(1);
         sia1(5):=zia1(19)+sia1(2);
         sia1(6):=sia1(5)+ans(2);
         for i:=4,5,6,7,8 do sia1(i+3):=ia(i);

         setshare6(zbs,sia1,1);
         ans1(9):=send(zbs,ans1);
         tofrom(ans,ans1,18);
      end else

      begin  <* anden op. *>
         getshare6(zbs,sia1,1);
         for i:=1 step 1 until 8 do sia1(i+3):=ia(i);
         setshare6(zbs,sia1,1);
         i:=send(zbs,ans1);
      end;

      ans(1):=ans1(1);
      monitor(22,zcom,j,ans); <* send answer *>

      write(out,<: <10>op:    :>,<<ddd>,ia(1) shift (-12),ia(1) extract 12,
                "sp",5,<: status: :>,<<ddddddd>,ans(1),<:<10>first: :>,ia(2),
                "sp",5,<:hw:     :>,ans(2),<:<10>last:  :>,ia(3),"sp",5,
                <:chars:  :>,ans(3),<:<10>segm.: :>,ia(4),"sp",5,
                <:result: :>,ans(9),"nl",1); 

   end;
TL:
end
